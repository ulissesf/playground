#!/usr/bin/env -S bpftrace

BEGIN
{
	@pids[$1] = $1;
}

tracepoint:sched:sched_process_fork
/@pids[pid]/
{
	$ppid = (uint64)args->parent_pid;
	if (pid != $ppid && $ppid == tid) {
	    printf("DBG: forking from thread %llu under process %llu\n", $ppid, pid);
	}

	$cpid = (uint64)args->child_pid;
	@pids[$cpid] = pid;
	printf("INF: Added pid %llu[%s] forked from %llu[%s] under overall process %llu\n", $cpid, args->child_comm, $ppid, args->parent_comm, pid);
}

tracepoint:sched:sched_process_exit
{
	$pid = (uint64)args->pid;
	if (@pids[$pid]) {
	    if ($pid != pid && $pid == tid) {
	        printf("DBG: exiting from thread %llu under process %llu\n", $pid, pid);
	    }
	    printf("INF: Removed pid %llu[%s] from under overall process %llu\n", $pid, args->comm, @pids[$pid]);
	    delete(@pids[$pid]);
	}
}
